apply plugin: 'com.android.library'
apply plugin: 'realm-android'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode HLJ_COMMON_LIBRARY_VER_CODE
        versionName HLJ_COMMON_LIBRARY_VERSION

        // ARouter支持
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
        buildConfigField "long", "BUILD_TIME_STAMP", new Date().time+"L"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    /*****************************本地module引入*******************************/
    implementation project(':PullToRefreshlibrary')
    implementation project(':SliderLibrary')
    implementation project(':wheelpickerlibrary')
    /*****************************本地module引入*******************************/

    /*****************************本地jar包引入*******************************/
    api files('libs/pinyin4j-2.5.0.jar')
    /*****************************本地jar包引入*******************************/

    /*****************************远程第三方库引入*******************************/
    api "com.android.support:design:$DESIGHT_VERSION"
    api "com.android.support:support-v4:$SUPPORT_V4_VERSION"
    api "com.android.support:cardview-v7:$CARDVIEW_V7_VERSION"
    api "com.android.support:appcompat-v7:$APP_COMPAT_V7_VERSION"
    api "com.android.support:gridlayout-v7:$GRID_LAYOUT_V7_VERSION"
    api "com.android.support:recyclerview-v7:$RECYCLERVIEW_V7_VERSION"
    implementation 'com.alibaba:arouter-api:1.2.1'
    implementation 'com.github.hotchemi:permissionsdispatcher:2.1.3'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.googlecode.mp4parser:isoparser:1.1.21'
    implementation 'com.nineoldandroids:library:2.4.0'
    //二维码扫描
    api 'cn.bingoogolapple:bga-qrcode-zxing:1.2.0'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.2.3'
    implementation 'joda-time:joda-time:2.9.6'
    /*****************************远程第三方库引入*******************************/

    /*****************************annotationProcessor*******************************/
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.2'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'
    /*****************************annotationProcessor*******************************/

}

publishing {
    publications {
        aar(MavenPublication) {
            groupId LIBRARY_PACKAGE_NAME
            version = android.defaultConfig.versionName
            artifactId project.getProject().getName()

            // Tell maven to prepare the generated "* .aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getProject().getName()}-release.aar")
        }
    }
}

artifactory {
    contextUrl = "$ARTIFACTORY_HOST/artifactory"
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = 'libs-release-local'

            username = "admin"
            password = "iwerther"
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

